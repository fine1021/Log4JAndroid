apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

afterEvaluate {
    tasks.withType(Javadoc) {
        failOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    provided 'org.slf4j:slf4j-api:1.7.25'
    provided 'org.apache.logging.log4j:log4j-slf4j-impl:2.3'
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    compile 'org.apache.logging.log4j:log4j-core:2.3'
    compile 'org.apache.commons:commons-compress:1.12'
    compile 'com.lmax:disruptor:3.3.6'
    testCompile 'junit:junit:4.12'
}

ext {
    USER_ORG = 'fine1021'
    REPO_NAME = 'maven'
    GROUP_ID = 'com.simple.android.log4j2'
    ARTIFACT_ID = 'log4j2-android'
    DESCRIPTION = 'a support log4j2 library for android'
    PUBLISH_VERSION = "${versionMajor}.${versionMinor}.${versionPatch}"
    WEB_SITE = 'https://github.com/fine1021/Log4JAndroid'
}

task binaryJar(dependsOn: ['compileReleaseJavaWithJavac'], type: Jar) {

    baseName = "log4j2-android"
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
//    classifier = "release"
    extension = "jar"

    // [baseName]-[appendix]-[version]-[classifier].[extension]
    // archiveName = "log4j2-android.jar"

    def srcClassDir = [project.buildDir.absolutePath + "/intermediates/classes/release"];
    from srcClassDir

    exclude "android/support/log4j2/BuildConfig.class"
    exclude "**/R.class"
    exclude "**/R\$*.class"
}

task sourcesJar(type: Jar) {

    baseName = "log4j2-android"
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
    classifier = "sources"
    extension = "jar"

    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    baseName = "log4j2-android"
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
    classifier = "javadoc"
    extension = "jar"

    from javadoc.destinationDir
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publish {
    userOrg = USER_ORG
    repoName = REPO_NAME
    groupId = GROUP_ID
    artifactId = ARTIFACT_ID
    publishVersion = PUBLISH_VERSION
    description = DESCRIPTION
    website = WEB_SITE
    issueTracker = "${website}/issues"
    repository = "${website}.git"
    bintrayUser = properties.getProperty("bintray.user")
    bintrayKey = properties.getProperty("bintray.apikey")
    dryRun = false
}
